#!/bin/sh

########################################################################

# This file is part of the UNAM telescope control system.

########################################################################

# Copyright Â© 2018, 2019 Alan M. Watson <alan@astro.unam.mx>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL
# WARRANTIES WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE
# AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL
# DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR
# PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER
# TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
# PERFORMANCE OF THIS SOFTWARE.

########################################################################

while read txt
do
  echo @START $txt
  cat $txt
  echo @END
done |
sed '
  /^@START /s/.txt$//
  /^@START /s/\// /g
  s/=/ /
' |
grep -E '^(@START|@END|CCD_NAME |EXPTIME |DATE-OBS |ACQTIME1 |SDATE |FILTA |SFL |STRSTRA |STRSTDE |STROBHA |STROBAM)' |
awk '
  
  function stringvalue(s) {
    sub("^'"'"'", "", s);
    sub("'"'"'$", "", s);
    gsub("'"''"'", "'"'"'", s);
    sub(" *$", "", s);
    gsub(",", "", s);
    return s;
  }

  $1 == "@START" {
    proposalid = $(NF - 2);
    visitid = $(NF - 1);
    filename = $(NF);
    channel = "";
    type = "";
    exptime = "";
    dateobs = "";
    acqtime1 = "";
    sdate = "";
    filter = "";
    alpha = "";
    delta = "";
    ha = "";
    X = "";
    if (filename ~ "C0") {
      channel = "C0";
    } else if (filename ~ "C1") {
      channel = "C1";
    } else if (filename ~ "C2") {
      channel = "C2";
    } else if (filename ~ "C3") {
      channel = "C3";
    }
  }
  $1 == "CCD_NAME" {
    channel = stringvalue($2);
  }
  $1 == "EXPTIME" {
    exptime = $2;
  }
  $1 == "DATE-OBS" {
    dateobs = substr(stringvalue($2) "000", 1, 23);
  }
  $1 == "ACQTIME1" {
    acqtime1 = substr(stringvalue($2) "000", 1, 23);
  }
  $1 == "SDATE" {
    sdate = substr(stringvalue($2) "000", 1, 23);
  }
  $1 == "FILTA" || $1 == "SFL" {
    filter = stringvalue($2);
  }
  $1 == "STRSTRA" {
    alpha = $2;
  }
  $1 == "STRSTDE" {
    delta = $2;
  }
  $1 == "STROBHA" {
    ha = $2;
  }
  $1 == "STROBAM" {
    X = $2;
  }
  $1 == "@END" {
    if (proposalid ~ "-0") {
      public = "true"
    } else {
      public = "false"
    }
    if (filename ~ "o.fits$") {
      type = "object";
    } else if (filename ~ "b.fits$") {
      type = "bias";
    } else if (filename ~ "d.fits$") {
      type = "dark";
    } else if (filename ~ "f.fits$") {
      type = "flat";
    }
    if ((channel == "C2" || channel == "C3") && acqtime1 != "") {
      dateobs = substr(acqtime1, 1, 10) "T" substr(acqtime1, 12);
    }
    if (dateobs == "") {
      dateobs = sdate;
    }
    if (channel == "C2") {
      filter = "ZY";
    } else if (channel == "C3") {
      filter = "JH";
    }
    printf("%s,%s,%d,%s,%s,%s,%.3f,%s,%.5f,%.5f,%.5f,%.3f,%s\n", 
      dateobs, proposalid, visitid, filename, 
      channel, type, exptime, filter,
      alpha, delta, ha, X, 
      public);
  }
' | 
sort -t, -k4
